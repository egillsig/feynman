<program>        : statement (<';;'> statement)* <';;'>?

<statement>      : <'def'> definition | tl-expression | unit-def

unit-def         : <'unit'> name | <'unit'> name <'='> unit-expr

<definition>     : def | func-def
def              : name (<':'> type-expr)? <'='> tl-expression
func-def         : name <'['> arg-list <']'> (<':'> type-expr)? <'='> tl-expression

(* Denotable types *)
<type-expr>      : x-type-expr | func-type
func-type        : x-type-expr <'->'> type-expr

<x-type-expr>    : prim-type-expr | product-type
product-type     : <'['> prim-type-expr (<','> prim-type-expr)+ <']'>

<prim-type-expr> : type-name | var-type | <'('> type-expr <')'> | unit-expr
type-name        : 'Bool'

<unit-expr>      : p-unit-expr | mul-units | div-units
mul-units        : unit-expr <'*'> p-unit-expr | unit-expr p-unit-expr
div-units        : unit-expr <'/'> p-unit-expr

<p-unit-expr>    : exp-unit | prim-unit-expr
exp-unit         : prim-unit-expr <'^'> exponent
<prim-unit-expr> : name | dimensionless | var-type | <'('> unit-expr <')'>
dimensionless    : <'1'>
var-type         : <"'"> name

<exponent>           : integer | unary-plus-exponent | unary-minus-exponent
unary-plus-exponent  : <'+'> exponent
unary-minus-exponent : <'-'> exponent

(* End denotable types *)

arg-list         : arg (<','> arg)* | epsilon
arg              : name (<':'> type-expr)?

<tl-expression>  : function | if | let | expr

function         : <'fn'> <'['> arg-list <']'> tl-expression
if               : <'if'> tl-expression <'then'> tl-expression <'else'> tl-expression
let              : <'let'> definition <'in'> tl-expression

<expr>           : a-expr | logic
logic            : a-expr logic-op a-expr

logic-op         : '<' | '>' | '<=' | '>=' | '==' | '!='

<a-expr>         : m_expr | add | subtract
add              : a-expr <'+'> m_expr
subtract         : a-expr <'-'> m_expr

<m_expr>         : first_u_expr | mul | div
mul              : m_expr <'*'> u_expr | m_expr u_expr
div              : m_expr <'/'> first_u_expr

(* Unary +/- operators can only appear as the first term in an m_expr
   to avoid ambiguity with space-as-multiplication and +/- as binary ops *)

<first_u_expr>   : unary_plus_expr | unary_minus_expr | u_expr
unary_plus_expr  : <'+'> first_u_expr
unary_minus_expr : <'-'> first_u_expr
<u_expr>         : p_expr | unary_not_expr
unary_not_expr   : <'!'> u_expr

<p_expr>         : exp | primary
exp              : primary <'^'> first_u_expr

<primary>        : name | number | boolean | apply | <'('> tl-expression <')'> 

apply            : primary <'['> argument-list <']'>
<argument-list>  : (expr (<','> argument-list)*)?

name             : !(keyword ' ') #'[a-zA-Z_][a-zA-Z0-9_]*'
number           : #'\d+(\.\d*)?'
integer          : #'\d+'
boolean          : 'True' | 'False'
keyword          : 'def' | 'fn' | 'if' | 'then' | 'else' | 'let' | 'in' | 'Bool' | 'unit'
